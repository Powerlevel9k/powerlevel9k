# vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8
################################################################
# @title powerlevel9k Default Engine
# @source [powerlevel9k](https://github.com/bhilburn/powerlevel9k)
##
# @authors
#   Ben Hilburn - [@bhilburn](https://github.com/bhilburn)
#   Dominik Ritter - [@dritter](https://github.com/dritter)
#   Christo Kotze - [@onaforeignshore](https://github.com/onaforeignshore)
##
# @info
#   This file contains the default generator for the powerlevel9k project.
##

################################################################
# DEFAULT ENGINE - Synchronous
################################################################

################################################################
# Prompt Segment Constructors
#
# Methodology behind user-defined variables overwriting colors:
#     The first parameter to the segment constructors is the calling function's
#     name. From this function name, we strip the "prompt_"-prefix and
#     uppercase it. This is then prefixed with "P9K_" and suffixed
#     with either "_BACKGROUND" or "_FOREGROUND", thus giving us the variable
#     name. So each new segment is user-overwritten by a variable following
#     this naming convention.
################################################################

# The `CURRENT_BG` variable is used to remember what the last BG color used was
# when building the left-hand prompt. Because the RPROMPT is created from
# right-left but reads the opposite, this isn't necessary for the other side.
CURRENT_BG='NONE'

p9k::set_default last_left_element_index 1
p9k::set_default P9K_WHITESPACE_BETWEEN_LEFT_SEGMENTS " "
# Begin a left prompt segment
# Takes four arguments:
#   * $1: Name of the function that was originally invoked (mandatory).
#         Necessary, to make the dynamic color-overwrite mechanism work.
#   * $2: The array index of the current segment
#   * $3: Background color
#   * $4: Foreground color
#   * $5: The segment content
#   * $6: An identifying icon (must be a key of the __P9K_ICONS array)
# The latter three can be omitted,
__p9k_left_prompt_segment() {
  local segment_name="${1#__p9k_}"
  local current_index=$2
  # Check if the segment should be joined with the previous one
  local joined
  __p9k_segment_should_be_joined $current_index $last_left_element_index "$P9K_LEFT_PROMPT_ELEMENTS" \
      && joined=true \
      || joined=false

  # Colors
  local backgroundColor="${3}"
  local foregroundColor="${4}"

  # Overwrite given background-color by user defined variable for this segment.
  local BACKGROUND_USER_VARIABLE=P9K_${(U)${segment_name}#prompt_}_BACKGROUND
  local BG_COLOR_MODIFIER=${(P)BACKGROUND_USER_VARIABLE}
  [[ -n $BG_COLOR_MODIFIER ]] && backgroundColor="$BG_COLOR_MODIFIER"

  # Overwrite given foreground-color by user defined variable for this segment.
  local FOREGROUND_USER_VARIABLE=P9K_${(U)${segment_name}#prompt_}_FOREGROUND
  local FG_COLOR_MODIFIER=${(P)FOREGROUND_USER_VARIABLE}
  [[ -n $FG_COLOR_MODIFIER ]] && foregroundColor="$FG_COLOR_MODIFIER"

  # Get color codes here to save some calls later on
  backgroundColor="$(p9k::get_color_code ${backgroundColor})"
  foregroundColor="$(p9k::get_color_code ${foregroundColor})"

  local background foreground
  [[ -n "${backgroundColor}" ]] && background="$(p9k::background_color ${backgroundColor})" || background="%k"
  [[ -n "${foregroundColor}" ]] && foreground="$(p9k::foreground_color ${foregroundColor})" || foreground="%f"

  if [[ $CURRENT_BG != 'NONE' ]] && ! p9k::is_same_color "${backgroundColor}" "$CURRENT_BG"; then
    echo -n "${background}%F{$CURRENT_BG}"
    if [[ $joined == false ]]; then
      # Middle segment
      echo -n "$(p9k::print_icon 'LEFT_SEGMENT_SEPARATOR')$P9K_WHITESPACE_BETWEEN_LEFT_SEGMENTS"
    fi
  elif p9k::is_same_color "$CURRENT_BG" "${backgroundColor}"; then
    # Middle segment with same color as previous segment
    # We take the current foreground color as color for our
    # subsegment (or the default color). This should have
    # enough contrast.
    local complement
    [[ -n "${foregroundColor}" ]] && complement="${foreground}" || complement="$(p9k::foreground_color $DEFAULT_COLOR)"
    echo -n "${background}${complement}"
    if [[ $joined == false ]]; then
      echo -n "$(p9k::print_icon 'LEFT_SUBSEGMENT_SEPARATOR')$P9K_WHITESPACE_BETWEEN_LEFT_SEGMENTS"
    fi
  else
    # First segment
    echo -n "${background}$P9K_WHITESPACE_BETWEEN_LEFT_SEGMENTS"
  fi

  local visual_identifier
  if [[ -n $6 ]]; then
    visual_identifier="$(p9k::print_icon $6)"
    if [[ -n "$visual_identifier" ]]; then
      # Add an whitespace if we print more than just the visual identifier.
      # To avoid cutting off the visual identifier in some terminal emulators (e.g., Konsole, st),
      # we need to color both the visual identifier and the whitespace.
      [[ -n "$5" ]] && visual_identifier="$visual_identifier "
      # Allow users to overwrite the color for the visual identifier only.
      local visual_identifier_color_variable=P9K_${(U)${segment_name}#prompt_}_VISUAL_IDENTIFIER_COLOR
      p9k::set_default $visual_identifier_color_variable "${foregroundColor}"
      visual_identifier="$(p9k::foreground_color ${(P)visual_identifier_color_variable})${visual_identifier}%f"
    fi
  fi

  # Print the visual identifier
  echo -n "${visual_identifier}"
  # Print the content of the segment, if there is any
  [[ -n "$5" ]] && echo -n "${foreground}${5}"
  echo -n "${P9K_WHITESPACE_BETWEEN_LEFT_SEGMENTS}"

  CURRENT_BG="${backgroundColor}"
  last_left_element_index=$current_index
}

# End the left prompt, closes the final segment.
__p9k_left_prompt_end() {
  if [[ -n $CURRENT_BG ]]; then
    echo -n "%k$(p9k::foreground_color ${CURRENT_BG})$(p9k::print_icon 'LEFT_SEGMENT_SEPARATOR')"
  else
    echo -n "%k"
  fi
  echo -n "%f$(p9k::print_icon 'LEFT_SEGMENT_END_SEPARATOR')"
  CURRENT_BG=''
}

CURRENT_RIGHT_BG='NONE'

p9k::set_default last_right_element_index 1
p9k::set_default P9K_WHITESPACE_BETWEEN_RIGHT_SEGMENTS " "
# Begin a right prompt segment
# Takes four arguments:
#   * $1: Name of the function that was originally invoked (mandatory).
#         Necessary, to make the dynamic color-overwrite mechanism work.
#   * $2: The array index of the current segment
#   * $3: Background color
#   * $4: Foreground color
#   * $5: The segment content
#   * $6: An identifying icon (must be a key of the __P9K_ICONS array)
# No ending for the right prompt segment is needed (unlike the left prompt, above).
__p9k_right_prompt_segment() {
  local segment_name="${1#__p9k_}"
  local current_index=$2

  # Check if the segment should be joined with the previous one
  local joined
  __p9k_segment_should_be_joined $current_index $last_right_element_index "$P9K_RIGHT_PROMPT_ELEMENTS" \
      && joined=true \
      || joined=false

  # Colors
  local backgroundColor="${3}"
  local foregroundColor="${4}"

  # Overwrite given background-color by user defined variable for this segment.
  local BACKGROUND_USER_VARIABLE=P9K_${(U)${segment_name}#prompt_}_BACKGROUND
  local BG_COLOR_MODIFIER=${(P)BACKGROUND_USER_VARIABLE}
  [[ -n $BG_COLOR_MODIFIER ]] && backgroundColor="$BG_COLOR_MODIFIER"

  # Overwrite given foreground-color by user defined variable for this segment.
  local FOREGROUND_USER_VARIABLE=P9K_${(U)${segment_name}#prompt_}_FOREGROUND
  local FG_COLOR_MODIFIER=${(P)FOREGROUND_USER_VARIABLE}
  [[ -n $FG_COLOR_MODIFIER ]] && foregroundColor="$FG_COLOR_MODIFIER"

  # Get color codes here to save some calls later on
  backgroundColor="$(p9k::get_color_code ${backgroundColor})"
  foregroundColor="$(p9k::get_color_code ${foregroundColor})"

  local background foreground
  [[ -n "${backgroundColor}" ]] && background="$(p9k::background_color ${backgroundColor})" || background="%k"
  [[ -n "${foregroundColor}" ]] && foreground="$(p9k::foreground_color ${foregroundColor})" || foreground="%f"

  # If CURRENT_RIGHT_BG is "NONE", we are the first right segment.

  if [[ "$CURRENT_RIGHT_BG" != "NONE" ]]; then
    # This is the closing whitespace for the previous segment
    echo -n "${P9K_WHITESPACE_BETWEEN_RIGHT_SEGMENTS}%f"
  fi

  if [[ $joined == false ]] || [[ "$CURRENT_RIGHT_BG" == "NONE" ]]; then
    if p9k::is_same_color "$CURRENT_RIGHT_BG" "${backgroundColor}"; then
      # Middle segment with same color as previous segment
      # We take the current foreground color as color for our
      # subsegment (or the default color). This should have
      # enough contrast.
      local complement
      [[ -n "${foregroundColor}" ]] && complement="${foreground}" || complement="$(p9k::foreground_color $DEFAULT_COLOR)"
      echo -n "$complement$(p9k::print_icon 'RIGHT_SUBSEGMENT_SEPARATOR')%f"
    else
      # Use the new Background Color as the foreground of the segment separator
      echo -n "$(p9k::foreground_color ${backgroundColor})$(p9k::print_icon 'RIGHT_SEGMENT_SEPARATOR')%f"
    fi
  fi

  local visual_identifier
  if [[ -n "$6" ]]; then
    visual_identifier="$(p9k::print_icon $6)"
    if [[ -n "$visual_identifier" ]]; then
      # Add an whitespace if we print more than just the visual identifier.
      # To avoid cutting off the visual identifier in some terminal emulators (e.g., Konsole, st),
      # we need to color both the visual identifier and the whitespace.
      [[ -n "$5" ]] && visual_identifier=" $visual_identifier"
      # Allow users to overwrite the color for the visual identifier only.
      local visual_identifier_color_variable=P9K_${(U)${segment_name}#prompt_}_VISUAL_IDENTIFIER_COLOR
      p9k::set_default $visual_identifier_color_variable "${foregroundColor}"
      visual_identifier="$(p9k::foreground_color ${(P)visual_identifier_color_variable})${visual_identifier}%f"
    fi
  fi

  echo -n "${background}${foreground}"

  # Print whitespace only if segment is not joined or first right segment
  [[ $joined == false ]] || [[ "$CURRENT_RIGHT_BG" == "NONE" ]] && echo -n "${P9K_WHITESPACE_BETWEEN_RIGHT_SEGMENTS}"

  # Print segment content if there is any
  [[ -n "$5" ]] && echo -n "${5}"
  # Print the visual identifier
  echo -n "${visual_identifier}"

  CURRENT_RIGHT_BG="${backgroundColor}"
  last_right_element_index=$current_index
}

################################################################
# Prompt Segment Definitions
################################################################

################################################################
# The 'custom` prompt provides a way for users to invoke commands and display
# the output in a segment.
__p9k_prompt_custom() {
  local segment_name="${3:u}"
  # Get content of custom segment
  local command="P9K_CUSTOM_${segment_name}"
  local segment_content="$(eval ${(P)command})"

  if [[ -n $segment_content ]]; then
    "__p9k_$1_prompt_segment" "${0}_${3:u}" "$2" $DEFAULT_COLOR_INVERTED $DEFAULT_COLOR "$segment_content" "CUSTOM_${segment_name}_ICON"
  fi
}

################################################################
# Prompt processing and drawing
################################################################
# Main prompt
__p9k_build_left_prompt() {
  local index=1
  local element
  for element in "${P9K_LEFT_PROMPT_ELEMENTS[@]}"; do
    # Remove joined information in direct calls
    element=${element%_joined}

    # Check if it is a custom command, otherwise interpet it as
    # a prompt.
    if [[ $element[0,7] =~ "custom_" ]]; then
      "__p9k_prompt_custom" "left" "$index" $element[8,-1]
    else
      "prompt_$element" "left" "$index"
    fi

    index=$((index + 1))
  done

  __p9k_left_prompt_end
}

# Right prompt
__p9k_build_right_prompt() {
  local index=1
  local element
  for element in "${P9K_RIGHT_PROMPT_ELEMENTS[@]}"; do
    # Remove joined information in direct calls
    element=${element%_joined}

    # Check if it is a custom command, otherwise interpet it as
    # a prompt.
    if [[ $element[0,7] =~ "custom_" ]]; then
      "__p9k_prompt_custom" "right" "$index" $element[8,-1]
    else
      "prompt_$element" "right" "$index"
    fi

    index=$((index + 1))
  done

  # Clear to the end of the line
  echo -n "%E"
}

__p9k_preexec() {
  _P9K_TIMER_START=$EPOCHREALTIME
}

p9k::set_default P9K_PROMPT_ADD_NEWLINE false
__p9k_prepare_prompts() {
  # Return values. These need to be global, because
  # they are used in prompt_status. Also, we need
  # to get the return value of the last command at
  # very first in this function. Do not move the
  # lines down, otherwise the last command is not
  # what you expected it to be.
  RETVAL=$?
  RETVALS=( "$pipestatus[@]" )

  local RPROMPT_SUFFIX RPROMPT_PREFIX
  _P9K_COMMAND_DURATION=$((EPOCHREALTIME - _P9K_TIMER_START))

  # Reset start time
  _P9K_TIMER_START=0x7FFFFFFF

  if [[ "$P9K_PROMPT_ON_NEWLINE" == true ]]; then
    PROMPT='$(p9k::print_icon 'MULTILINE_FIRST_PROMPT_PREFIX')%f%b%k$(__p9k_build_left_prompt)
$(p9k::print_icon 'MULTILINE_LAST_PROMPT_PREFIX')'
    if [[ "$P9K_RPROMPT_ON_NEWLINE" != true ]]; then
      # The right prompt should be on the same line as the first line of the left
      # prompt. To do so, there is just a quite ugly workaround: Before zsh draws
      # the RPROMPT, we advise it, to go one line up. At the end of RPROMPT, we
      # advise it to go one line down. See:
      # http://superuser.com/questions/357107/zsh-right-justify-in-ps1
      local LC_ALL="" LC_CTYPE="en_US.UTF-8" # Set the right locale to protect special characters
      RPROMPT_PREFIX='%{'$'\e[1A''%}' # one line up
      RPROMPT_SUFFIX='%{'$'\e[1B''%}' # one line down
    else
      RPROMPT_PREFIX=''
      RPROMPT_SUFFIX=''
    fi
  else
    PROMPT='%f%b%k$(__p9k_build_left_prompt)'
    RPROMPT_PREFIX=''
    RPROMPT_SUFFIX=''
  fi

  if [[ "$P9K_DISABLE_RPROMPT" != true ]]; then
    RPROMPT="${RPROMPT_PREFIX}"'%f%b%k$(__p9k_build_right_prompt)%{$reset_color%}'"${RPROMPT_SUFFIX}"
  fi

local NEWLINE='
'

  if [[ $P9K_PROMPT_ADD_NEWLINE == true ]]; then
    NEWLINES=""
    repeat ${P9K_PROMPT_ADD_NEWLINE_COUNT:-1} { NEWLINES+=$NEWLINE }
    PROMPT="$NEWLINES$PROMPT"
  fi

  # Allow iTerm integration to work
  [[ $ITERM_SHELL_INTEGRATION_INSTALLED == "Yes" ]] && PROMPT="%{$(iterm2_prompt_mark)%}$PROMPT"
}

zle-keymap-select () {
	zle reset-prompt
	zle -R
}

p9k::set_default P9K_IGNORE_TERM_COLORS false
p9k::set_default P9K_IGNORE_TERM_LANG false

prompt_powerlevel9k_setup() {
  # The value below was set to better support 32-bit CPUs.
  # It's the maximum _signed_ integer value on 32-bit CPUs.
  # Please don't change it until 19 January of 2038. ;)

  # Disable false display of command execution time
  _P9K_TIMER_START=0x7FFFFFFF

  # The prompt function will set these prompt_* options after the setup function
  # returns. We need prompt_subst so we can safely run commands in the prompt
  # without them being double expanded and we need prompt_percent to expand the
  # common percent escape sequences.
  prompt_opts=(cr percent sp subst)

  # Borrowed from promptinit, sets the prompt options in case the theme was
  # not initialized via promptinit.
  setopt noprompt{bang,cr,percent,sp,subst} "prompt${^prompt_opts[@]}"

  # Display a warning if the terminal does not support 256 colors
  __p9k_term_colors

  # If the terminal `LANG` is set to `C`, this theme will not work at all.
  if [[ $P9K_IGNORE_TERM_LANG == false ]]; then
    local term_lang
    term_lang=$(echo $LANG)
    if [[ $term_lang == 'C' ]]; then
      print -P "\t%F{red}WARNING!%f Your terminal's 'LANG' is set to 'C', which breaks this theme!"
      print -P "\t%F{red}WARNING!%f Please set your 'LANG' to a UTF-8 language, like 'en_US.UTF-8'"
      print -P "\t%F{red}WARNING!%f _before_ loading this theme in your \~\.zshrc. Putting"
      print -P "\t%F{red}WARNING!%f %F{blue}export LANG=\"en_US.UTF-8\"%f at the top of your \~\/.zshrc is sufficient."
    fi
  fi

  # initialize colors
  autoload -U colors && colors

  # initialize timing functions
  zmodload zsh/datetime

  # Initialize math functions
  zmodload zsh/mathfunc

  # initialize hooks
  autoload -Uz add-zsh-hook

  # prepare prompts
  add-zsh-hook precmd __p9k_prepare_prompts
  add-zsh-hook preexec __p9k_preexec

  zle -N zle-keymap-select
}

prompt_powerlevel9k_teardown() {
  add-zsh-hook -D precmd __p9k_\*
  add-zsh-hook -D preexec __p9k_\*
  PROMPT='%m%# '
  RPROMPT=
}
