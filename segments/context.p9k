#!/usr/bin/env zsh
# vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8
################################################################
# @title powerlevel9k Segment - Context
# @source [powerlevel9k](https://github.com/bhilburn/powerlevel9k)
##

################################################################
# Register segment
# Parameters:
#   segment_name  context  foreground  background  Generic  Flat/Awesome-Patched  Awesome-FontConfig  Awesome-Mapped-FontConfig  NerdFont
#
registerSegment 'CONTEXT' 'ROOT'        "${DEFAULT_COLOR}" "yellow" ""  ""  ""  ""  ""  ""
registerSegment 'CONTEXT' 'SUDO'        "${DEFAULT_COLOR}" "yellow" ""  ""  ""  ""  ""  ""
registerSegment 'CONTEXT' 'DEFAULT'     "${DEFAULT_COLOR}" "yellow" ""  ""  ""  ""  ""  ""
registerSegment 'CONTEXT' 'REMOTE'      "${DEFAULT_COLOR}" "yellow" ""  ""  ""  ""  ""  ""
registerSegment 'CONTEXT' 'REMOTE_SUDO' "${DEFAULT_COLOR}" "yellow" ""  ""  ""  ""  ""  ""

setDefault P9K_CONTEXT_ALWAYS_SHOW false
setDefault P9K_CONTEXT_ALWAYS_SHOW_USER false
setDefault P9K_CONTEXT_TEMPLATE "%n@%m"
################################################################
# @description
#   Displays the context - user@hostname (who am I and where am I).
##
# @args
#   $1 string Alignment - left | right
#   $2 integer Segment index
#   $3 boolean Whether the segment should be joined
##
# @note
#    If $DEFAULT_USER is not set, this prompt segment will always print.
##
prompt_context() {
  local current_state="DEFAULT"
  local content=""

  if [[ "$P9K_CONTEXT_ALWAYS_SHOW" == true ]] || [[ "$(whoami)" != "$DEFAULT_USER" ]] || [[ -n "$SSH_CLIENT" || -n "$SSH_TTY" ]]; then
      content="${P9K_CONTEXT_TEMPLATE}"
  elif [[ "$P9K_CONTEXT_ALWAYS_SHOW_USER" == true ]]; then
      content="$(whoami)"
  else
      return
  fi

  if [[ $(print -P "%#") == '#' ]]; then
    current_state="ROOT"
  elif [[ -n "$SSH_CLIENT" || -n "$SSH_TTY" ]]; then
    if sudo -n true 2>/dev/null; then
      current_state="REMOTE_SUDO"
    else
      current_state="REMOTE"
    fi
  elif sudo -n true 2>/dev/null; then
    current_state="SUDO"
  fi

  p9kPrepareSegment "$0" "${(U)current_state}" $1 "$2" $3 "${content}"
}
