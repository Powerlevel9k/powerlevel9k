# vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8
################################################################
# @title powerlevel9k Segment - Python Environment
# @source [powerlevel9k](https://github.com/bhilburn/powerlevel9k)
##

(){
  # Set the right locale to protect special characters
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  ################################################################
  # Register segment
  # Parameters:
  #   segment_name  context  background  foreground  Generic  Flat/Awesome-Patched  Awesome-FontConfig  Awesome-Mapped-FontConfig  NerdFont
  #                                                            Óòº          Óòº         üêç          Óúº
  p9k::register_segment "PYENV" "" "blue" "$DEFAULT_COLOR" ''  $'\uE63C'  $'\uE63C'  $'\U1F40D'  $'\uE73C '

  ################################################################
  # Register segment default values
  p9k::set_default P9K_PYENV_PROMPT_ALWAYS_SHOW false
}

################################################################
# @description
#   Display the current active python version (with restrictions).
##
# @args
#   $1 string Alignment - left | right
#   $2 integer Segment index
#   $3 boolean Whether the segment should be joined
##
# @see
#   [Choosing the Python Version](https://github.com/pyenv/pyenv#choosing-the-python-version)
##
prompt_pyenv() {
  if [[ -n "$PYENV_VERSION" ]]; then
    "__p9k_$1_prompt_segment" "$0" "$2" "blue" "$DEFAULT_COLOR" "$PYENV_VERSION" 'PYTHON_ICON'
  elif [ $commands[pyenv] ]; then
    local pyenv_version_name="$(pyenv version-name)"
    local pyenv_global="system"
    local pyenv_root="$(pyenv root)"
    if [[ -f "${pyenv_root}/version" ]]; then
      pyenv_global="$(pyenv version-file-read ${pyenv_root}/version)"
    fi
    if [[ "${pyenv_version_name}" != "${pyenv_global}" || "${P9K_PYENV_PROMPT_ALWAYS_SHOW}" == "true" ]]; then
      p9k::prepare_segment "$0" "" $1 "$2" $3 "${pyenv_version_name}"
    fi
  fi
}
