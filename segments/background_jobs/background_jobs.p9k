# vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8
################################################################
# @title powerlevel9k Segment - Background Jobs
# @source [powerlevel9k](https://github.com/bhilburn/powerlevel9k)
##

(){
  # Set the right locale to protect special characters
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  ################################################################
  # Register segment
  # Parameters:
  #   segment_name  context  background  foreground  Generic  Flat/Awesome-Patched  Awesome-FontConfig  Awesome-Mapped-FontConfig  NerdFont
  #                                                                   ⚙                                                               
  p9k::register_segment 'BACKGROUND_JOBS' '' "yellow" "black" $'\u2699'  $'\uE82F '  $'\uF013 '  '\u'$CODEPOINT_OF_AWESOME_COG' '  $'\uF013 '

  p9k::set_default P9K_BACKGROUND_JOBS_VERBOSE true
  p9k::set_default P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS false
  p9k::set_default P9K_BACKGROUND_JOBS_EXPANDED false
}

################################################################
# @description
#   Displays the number of background jobs with an icon.
##
# @args
#   $1 string Alignment - left | right
#   $2 integer Segment index
#   $3 boolean Whether the segment should be joined
##
prompt_background_jobs() {
  local raw_jobs=( "${(@f)$(command jobs -l)}" )
  local -a suspended=( "${(@M)raw_jobs:#*suspended*}" )
  local -a running=( "${(@M)raw_jobs:#*running*}" )
  local suspended_count=${#suspended}
  local running_count=${#running}
  local jobs_total=$(( suspended_count + running_count ))
  local segment_content="${jobs_total}"

  if [[ "${(L)P9K_BACKGROUND_JOBS_VERBOSE}" == "true" || "${(L)P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS}" == "true" ]]; then
    if [[ "${(L)P9K_BACKGROUND_JOBS_EXPANDED}" == "true" ]]; then
      segment_content="${running_count}r ${suspended_count}s"
    fi
  elif (( jobs_total == 1 )); then
    # Show only the icon, if P9K_BACKGROUND_JOBS_VERBOSE is not set and there is only one background job
    segment_content=""
  fi

  p9k::prepare_segment "$0" "" $1 "$2" $3 "${segment_content}" \
      "[[ ${(L)P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS} == true ]] || (( ${jobs_total} > 0 ))"
}
