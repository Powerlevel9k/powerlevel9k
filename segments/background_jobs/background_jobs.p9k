# vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8
################################################################
# @title powerlevel9k Segment - Background Jobs
# @source [powerlevel9k](https://github.com/bhilburn/powerlevel9k)
##

(){
  # Set the right locale to protect special characters
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  ################################################################
  # Register segment
  # Parameters:
  #   segment_name  context  background  foreground  Generic  Flat/Awesome-Patched  Awesome-FontConfig  Awesome-Mapped-FontConfig  NerdFont
  #                                                                   ⚙                                                               
  p9k::register_segment 'BACKGROUND_JOBS' '' "yellow" "black" $'\u2699'  $'\uE82F '  $'\uF013 '  '\u'$CODEPOINT_OF_AWESOME_COG' '  $'\uF013 '

  p9k::set_default P9K_BACKGROUND_JOBS_VERBOSE true
  p9k::set_default P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS false
  p9k::set_default P9K_BACKGROUND_JOBS_EXPANDED false
}

__p9k_background_jobs() {
  # See https://unix.stackexchange.com/questions/68571/show-jobs-count-only-if-it-is-more-than-0
  jobs_running=${(M)#${jobstates%%:*}:#running}
  jobs_suspended=${(M)#${jobstates%%:*}:#suspended}
}

TRAPCLD() {
  zle -I && __p9k_background_jobs && __p9k_prepare_prompts && zle .reset-prompt && zle -R
}

# initialize hooks
autoload -Uz add-zsh-hook
add-zsh-hook precmd __p9k_background_jobs
################################################################
# @description
#   Displays the number of background jobs with an icon.
##
# @args
#   $1 string Alignment - left | right
#   $2 integer Segment index
#   $3 boolean Whether the segment should be joined
##
prompt_background_jobs() {
  local jobs_print=""
  local -r total_jobs=$(( jobs_running + jobs_suspended ))

  if [[ "${(L)P9K_BACKGROUND_JOBS_VERBOSE}" == "true" || "${(L)P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS}" == "true" ]]; then
    jobs_print=0
    if (( ${total_jobs} > 0 )); then
      if [[ "${(L)P9K_BACKGROUND_JOBS_EXPANDED}" == "true" ]]; then
        jobs_print="${jobs_running:-0}r ${jobs_suspended:-0}s"
      else
        jobs_print="${total_jobs}"
      fi
    fi
  fi

  p9k::prepare_segment "$0" "" $1 "$2" $3 "${jobs_print}" \
      "[[ ${(L)P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS} == true ]] || (( ${total_jobs} > 0 ))"
}
