# vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8
################################################################
# @title powerlevel9k Segment - IP
# @source [powerlevel9k](https://github.com/bhilburn/powerlevel9k)
##

################################################################
# Register segment
# Parameters:
#   segment_name  context  foreground  background  Generic  Flat/Awesome-Patched  Awesome-FontConfig  Awesome-Mapped-FontConfig  NerdFont
#                                                                                                                 
p9k::register_segment "IP" "" "cyan" "$DEFAULT_COLOR"  'IP'  $'\uE1AD'  $'\uF09E'  '\u'${CODEPOINT_OF_AWESOME_RSS}  $'\uF1EB'

################################################################
# @description
#   Display the current IP address.
##
# @args
#   $1 string Alignment - left | right
#   $2 integer Segment index
#   $3 boolean Whether the segment should be joined
##
prompt_ip() {
  if [[ "$__P9K_OS" == "OSX" ]]; then
    if p9k::defined P9K_IP_INTERFACE; then
      # Get the IP address of the specified interface.
      ip=$(ipconfig getifaddr "$P9K_IP_INTERFACE")
    else
      local interfaces callback
      # Get network interface names ordered by service precedence.
      interfaces=$(networksetup -listnetworkserviceorder | grep -o "Device:\s*[a-z0-9]*" | grep -o -E '[a-z0-9]*$')
      callback='ipconfig getifaddr $item'

      ip=$(p9k::get_relevant_item "$interfaces" "$callback")
    fi
  else
    if p9k::defined P9K_IP_INTERFACE; then
      # Get the IP address of the specified interface.
      ip=$(ip -4 a show "$P9K_IP_INTERFACE" | grep -o "inet\s*[0-9.]*" | grep -o -E "[0-9.]+")
    else
      local interfaces callback
      # Get all network interface names that are up
      interfaces=$(ip link ls up | grep -o -E ":\s+[a-z0-9]+:" | grep -v "lo" | grep -o -E "[a-z0-9.]+")
      callback='ip -4 a show ${item} | grep -o -E "inet\s*[0-9.]+" | grep -o -E "[0-9.]+"'

      ip=$(p9k::get_relevant_item "$interfaces" "$callback")
    fi
  fi

  p9k::prepare_segment "$0" "" $1 "$2" $3 "$ip"
}
