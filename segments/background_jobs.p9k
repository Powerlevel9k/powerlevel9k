#!/usr/bin/env zsh
# vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8
################################################################
# @title powerlevel9k Segment - Background Jobs
# @source [powerlevel9k](https://github.com/bhilburn/powerlevel9k)
##

################################################################
# Register segment
# Parameters:
#   segment_name  context  foreground  background  Generic  Flat/Awesome-Patched  Awesome-FontConfig  Awesome-Mapped-FontConfig  NerdFont
#                                                            ⚙                                                               
registerSegment 'BACKGROUND_JOBS' '' "$DEFAULT_COLOR" "cyan" $'\u2699'  $'\uE82F '  $'\uF013 '  '\u'$CODEPOINT_OF_AWESOME_COG' '  $'\uF013 '

p9kBackgroundJobs() {
  # See https://unix.stackexchange.com/questions/68571/show-jobs-count-only-if-it-is-more-than-0
  jobs_running=${(M)#${jobstates%%:*}:#running}
  jobs_suspended=${(M)#${jobstates%%:*}:#suspended}
}

add-zsh-hook precmd p9kBackgroundJobs

setDefault P9K_BACKGROUND_JOBS_VERBOSE false
setDefault P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS false
setDefault P9K_BACKGROUND_JOBS_EXPANDED false
################################################################
# @description
#   Displays the number of background jobs with an icon.
##
# @args
#   $1 string Alignment - left | right
#   $2 integer Segment index
#   $3 boolean Whether the segment should be joined
##
prompt_background_jobs() {
  local jobs_print=""
  local total=$(( ${jobs_running} + ${jobs_suspended} ))
  [[ "${(L)P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS}" == "true" ]] && jobs_print=0

  if [[ "${(L)P9K_BACKGROUND_JOBS_VERBOSE}" == "true" || "${(L)P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS}" == "true" ]] \
      && (( ${total} > 0 )); then
    if [[ "${(L)P9K_BACKGROUND_JOBS_EXPANDED}" == "true" ]]; then
      jobs_print="${jobs_running}r ${jobs_suspended}s"
    else
      jobs_print="${total}"
    fi
  fi

  p9kPrepareSegment "$0" "" $1 "$2" $3 "${jobs_print}" \
      "[[ ${(L)P9K_BACKGROUND_JOBS_VERBOSE_ALWAYS} == true ]] || (( ${total} > 0 ))"
}
